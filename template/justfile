set positional-arguments := true

all: build test lint

build:
  cabal v2-build

test: doctest spec-test integration-test

doctest:
  cabal v2-test doctest

spec-test:
  cabal v2-test spec

integration-test:
  #!/usr/bin/env bash
  header() {
    if [[ -t 1 ]]; then
      echo -e "\e[1;4m$*\e[0m"
    else
      echo "$*"
    fi
  }
  trap 'header [Cleaning up...]; arion down' EXIT
  header "[Building containerized test environment...]"
  arion build
  header "[Starting containerized test environment...]"
  arion up -d webserver
  header "[Running tests...]"
  arion run test

lint:
  hlint app lib
  weeder

clean:
  cabal v2-clean

format: format-src format-cabal

format-src:
  find . \
    -regex '\./\(app\|lib\|test\|benchmark\).*\.hs' \
    -exec fourmolu -i {} +

format-cabal:
  cabal-fmt *.cabal -i

doc:
  cabal v2-haddock --haddock-hyperlink-source

bench:
  cabal v2-bench
